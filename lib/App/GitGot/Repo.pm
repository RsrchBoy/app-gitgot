package App::GitGot::Repo;

# ABSTRACT: Base repository objects
use Moo;
use MooX::Types::MooseLike::Base ':all';
use 5.010;

use namespace::clean -except => 'meta';

has 'label' => (
  is       => 'ro' ,
  isa      => Str ,
);

has 'name' => (
  is          => 'ro',
  isa         => Str,
  required    => 1 ,
);

has 'number' => (
  is          => 'ro',
  isa         => Int,
  required    => 1 ,
);

has 'path' => (
  is          => 'ro',
  isa         => Str,
  required    => 1 ,
);

has 'repo' => (
  is          => 'ro',
  isa         => Str,
);

has 'tags' => (
  is          => 'ro',
  isa         => Str,
);

has 'type' => (
  is          => 'ro',
  isa         => Str,
  required    => 1 ,
);

sub BUILDARGS {
  my( $class , $args ) = @_;

  my $count = $args->{count} || 0;

  die "Must provide entry" unless
    my $entry = $args->{entry};

  my $repo = $entry->{repo} //= '';

  if ( ! defined $entry->{name} ) {
    $entry->{name} = ( $repo =~ m|([^/]+).git$| ) ? $1 : '';
  }

  $entry->{tags} //= '';

  my $return = {
    number => $count ,
    name   => $entry->{name} ,
    path   => $entry->{path} ,
    repo   => $repo ,
    type   => $entry->{type} ,
    tags   => $entry->{tags} ,
  };

  $return->{label} = $args->{label} if $args->{label};

  return $return;
}


=method in_writable_format

Returns a serialized representation of the repository for writing out in a
config file.

=cut
sub in_writable_format {
  my $self = shift;

  my $writeable = {
    name => $self->name ,
    path => $self->path ,
  };

  foreach ( qw/ repo tags type /) {
    $writeable->{$_} = $self->$_ if $self->$_;
  }

  return $writeable;
}


__PACKAGE__->meta->make_immutable;
1;
